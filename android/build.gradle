plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.multiplatform'
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.toxicbakery.mppcalculator"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
}

dependencies {
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    testImplementation 'junit:junit:4.12'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation("com.ToxicBakery.logging:arbor-android:1.20") {
        exclude module: 'arbor-jvm'
    }
    implementation(project(':common')) {
        exclude module: 'arbor-jvm'
    }
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')
    }
}

android.applicationVariants.all { variant ->
    def variantNameCaps = variant.name.capitalize()
    tasks.create(
            name: "adbRun$variantNameCaps",
            dependsOn: "install$variantNameCaps",
            type: Exec
    ) {
        commandLine("adb", "shell", "am", "start", "-n", "com.toxicbakery.mppcalculator/com.toxicbakery.mppcalculator.MainActivity")
    }
}

